PREFIX myp: <https://elections.ontotext.com/resource/prop/indirect/>
PREFIX myd: <https://elections.ontotext.com/resource/prop/direct/>
PREFIX election: <https://elections.ontotext.com/resource/election/>
PREFIX myps: <https://elections.ontotext.com/resource/prop/statement/>
PREFIX mypq: <https://elections.ontotext.com/resource/prop/qualifier/>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX metaSection: <https://elections.ontotext.com/resource/metaSection/>
select
?mir_num
?mir_label
?mun_label
?place_label
?sec_id
?VOTERS_04
?VOTED_04
(?VOTED_04/?VOTERS_04 as ?ACTIVITY_04)
?VOTERS_11
?VOTED_11
(?VOTED_11/?VOTERS_11 as ?ACTIVITY_11)
(?VOTERS_11-?VOTERS_04 as ?VOTERS_DELTA)
(?VOTED_11-?VOTED_04 as ?VOTED_DELTA)
(?ACTIVITY_11-?ACTIVITY_04 as ?ACTIVITY_DELTA)
?BSP_04
?BSP_11
(?BSP_04/?VOTED_04 as ?BSP_RATIO_04)
(?BSP_11/?VOTED_11 as ?BSP_RATIO_11)
(?BSP_11-?BSP_04 as ?BSP_DELTA)
(?BSP_RATIO_11-?BSP_RATIO_04 as ?BSP_RATIO_DELTA)
?DB_04
?DB_11
(?DB_04/?VOTED_04 as ?DB_RATIO_04)
(?DB_11/?VOTED_11 as ?DB_RATIO_11)
(?DB_11-?DB_04 as ?DB_DELTA)
(?DB_RATIO_11-?DB_RATIO_04 as ?DB_RATIO_DELTA)
?DPS_04
?DPS_11
(?DPS_04/?VOTED_04 as ?DPS_RATIO_04)
(?DPS_11/?VOTED_11 as ?DPS_RATIO_11)
(?DPS_11-?DPS_04 as ?DPS_DELTA)
(?DPS_RATIO_11-?DPS_RATIO_04 as ?DPS_RATIO_DELTA)
?GERB_04
?GERB_11
(?GERB_04/?VOTED_04 as ?GERB_RATIO_04)
(?GERB_11/?VOTED_11 as ?GERB_RATIO_11)
(?GERB_11-?GERB_04 as ?GERB_DELTA)
(?GERB_RATIO_11-?GERB_RATIO_04 as ?GERB_RATIO_DELTA)
?ITN_04
?ITN_11
(?ITN_04/?VOTED_04 as ?ITN_RATIO_04)
(?ITN_11/?VOTED_11 as ?ITN_RATIO_11)
(?ITN_11-?ITN_04 as ?ITN_DELTA)
(?ITN_RATIO_11-?ITN_RATIO_04 as ?ITN_RATIO_DELTA)
?PP_04
?PP_11
(?PP_04/?VOTED_04 as ?PP_RATIO_04)
(?PP_11/?VOTED_11 as ?PP_RATIO_11)
(?PP_11-?ITN_11 as ?PP_DELTA)
(?PP_RATIO_11-?PP_RATIO_04 as ?PP_RATIO_DELTA)
{
    ?place myd:municipality ?mun ; rdfs:label ?place_label .
    ?mun rdfs:label ?mun_label .
    optional {?mun myd:mir ?mir . ?mir myd:number ?mir_num ; rdfs:label ?mir_label .}
    bind(strafter(str(?ms),str(metaSection:)) as ?sec_id)
    {
        select
        ?place ?ms
        (sum(?voters_count) as ?VOTERS_04)
        (sum(?voters_voted) as ?VOTED_04)
        (sum(?votes_bsp) as ?BSP_04)
        (sum(?votes_gerb) as ?GERB_04)
        (sum(?votes_itn) as ?ITN_04)
        (sum(?votes_dps) as ?DPS_04)
        (sum(?votes_db) as ?DB_04)
        (0 as ?PP_04)
        where {
            #           wd:Q164242  #DPS
            #           wd:Q133968 #GERB
            #        	wd:Q98098908 #ITN
            #        	wd:Q752259 #BSP
            #			wd:Q62808154 #DB
            ?voting myp:vote ?vote_dps, ?vote_bsp , ?vote_gerb, ?vote_itn, ?vote_db ;
                    myd:voters_count ?voters_count ;
                    myd:voters_voted_count ?voters_voted ;
                    myd:main_election election:pi2021
            ;
                    myd:section ?section;
            .
    		?section myd:place ?place ; myd:meta_section ?ms .

            ?vote_bsp myps:vote/myd:party/myd:party wd:Q752259;
                                         mypq:valid_votes_recieved ?votes_bsp ;
                                         .
            ?vote_dps myps:vote/myd:party/myd:party wd:Q164242;
                                         mypq:valid_votes_recieved ?votes_dps ;
                                         .
            ?vote_gerb myps:vote/myd:party/myd:party wd:Q133968;
                                          mypq:valid_votes_recieved ?votes_gerb ;
                                          .
            ?vote_itn myps:vote/myd:party/myd:party wd:Q98098908;
                                         mypq:valid_votes_recieved ?votes_itn ;
                                         .
            ?vote_db  myps:vote/myd:party/myd:party wd:Q62808154;
                                         mypq:valid_votes_recieved ?votes_db ;
                                           .
            #    	?main_party rdfs:label ?party_label .
        } group by ?place ?ms
    }
    {
        select
        ?place ?ms
        (sum(?voters_count) as ?VOTERS_11)
        (sum(?voters_voted) as ?VOTED_11)
        (sum(?votes_bsp) as ?BSP_11)
        (sum(?votes_gerb) as ?GERB_11)
        (sum(?votes_itn) as ?ITN_11)
        (sum(?votes_dps) as ?DPS_11)
        (sum(?votes_db) as ?DB_11)
        (sum(?votes_pp) as ?PP_11)
        where {
            #           wd:Q164242  #DPS
            #           wd:Q133968 #GERB
            #        	wd:Q98098908 #ITN
            #        	wd:Q752259 #BSP
            #			wd:Q62808154 #DB
            ?voting myp:vote ?vote_dps, ?vote_bsp , ?vote_gerb, ?vote_itn, ?vote_db, ?vote_pp ;
                    myd:voters_count ?voters_count ;
                    myd:voters_voted_count ?voters_voted ;
                    myd:main_election election:pi2021_11
            ;
          	myd:section ?section;
            .
    		?section myd:place ?place; myd:meta_section ?ms .

            ?vote_bsp myps:vote/myd:party/myd:party wd:Q752259;
                                         mypq:valid_votes_recieved ?votes_bsp ;
                                         .
            ?vote_dps myps:vote/myd:party/myd:party wd:Q164242;
                                         mypq:valid_votes_recieved ?votes_dps ;
                                         .
            ?vote_gerb myps:vote/myd:party/myd:party wd:Q133968;
                                          mypq:valid_votes_recieved ?votes_gerb ;
                                          .
            ?vote_itn myps:vote/myd:party/myd:party wd:Q98098908;
                                         mypq:valid_votes_recieved ?votes_itn ;
                                         .
            ?vote_db  myps:vote/myd:party/myd:party wd:Q62808154;
                                         mypq:valid_votes_recieved ?votes_db ;
                                           .
            ?vote_pp  myps:vote/myd:party/myd:party wd:Q108601789;
                                         mypq:valid_votes_recieved ?votes_pp ;
                                         .
            #    	?main_party rdfs:label ?party_label .
        } group by ?place ?ms
    }
}