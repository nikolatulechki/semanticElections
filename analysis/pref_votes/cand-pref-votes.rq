# pref votes for a single candidate

BASE  <https://elections.ontotext.com/resource/>
PREFIX my: <https://elections.ontotext.com/resource/entity/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX myd: <https://elections.ontotext.com/resource/prop/direct/>
PREFIX election: <https://elections.ontotext.com/resource/election/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX myps: <https://elections.ontotext.com/resource/prop/statement/>
PREFIX mypq: <https://elections.ontotext.com/resource/prop/qualifier/>
PREFIX myp: <https://elections.ontotext.com/resource/prop/indirect/>
select ?party_label ?cand_number ?cand_name ?obl ?mun ?place ?sec_id ?cand_preferences ?party_votes ?pref_ratio ?total_voted ?party_ratio ?protocol where {
    bind(<https://elections.ontotext.com/resource/candidate/pi2021_11/12/17/101> as ?candidate)
    ?candidate a my:Candidate ; myd:represents ?localParty  ; myd:candidacy ?el ; rdfs:label ?cand_name ; myd:number ?cand_number .
    ?voting myp:preference_vote ?pv ; myp:vote ?v ;  myd:section ?section ; myd:link_html ?protocol ; myd:voters_voted_count ?total_voted .
    ?v myps:vote ?localParty ; mypq:valid_votes_recieved ?party_votes .
    ?pv myps:preference_vote ?candidate ; mypq:valid_votes_recieved ?cand_preferences .
    ?localParty rdfs:label ?party_label .
    ?section myd:number ?sec_id ;
             myd:place/rdfs:label ?place ;
             myd:place/myd:municipality/rdfs:label ?mun ;
             myd:place/myd:municipality/myd:province/rdfs:label ?obl ;
    .

    bind(floor((?cand_preferences/?party_votes)*10000)/100 as ?pref_ratio)
    bind(floor((?party_votes/?total_voted)*10000)/100 as ?party_ratio)

} order by desc(?cand_preferences)