PREFIX my: <https://elections.ontotext.com/resource/entity/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX geo: <http://www.opengis.net/ont/geosparql#>
PREFIX myd: <https://elections.ontotext.com/resource/prop/direct/>
PREFIX election: <https://elections.ontotext.com/resource/election/>
PREFIX myp: <https://elections.ontotext.com/resource/prop/indirect/>
PREFIX mypq: <https://elections.ontotext.com/resource/prop/qualifier/>
PREFIX myps: <https://elections.ontotext.com/resource/prop/statement/>
PREFIX wd: <http://www.wikidata.org/entity/>
select
?ge
?ge_label
?district_label
(SUM(?VOTERS_PI21_11) as ?VOTERS_PI21_11_GE)
(SUM(?VOTERS_PI22)    as ?VOTERS_PI22_GE)
(SUM(?VOTERS_PI23)    as ?VOTERS_PI23_GE)

(SUM(?VOTED_PI21_11)  as ?VOTED_PI21_11_GE)
(SUM(?VOTED_PI22)     as ?VOTED_PI22_GE)
(SUM(?VOTED_PI23)     as ?VOTED_PI23_GE)

(SUM(?PPDB_PI23)     as ?PPDB_PI23_GE)
(SUM(?PP_PI22)     as ?PP_PI22_GE)
(SUM(?DB_PI22)     as ?DB_PI22_GE)
(?PP_PI22_GE+?DB_PI22_GE as ?SUM_PPDB_PI22_GE)
(SUM(?PP_PI21_11)     as ?PP_PI21_11_GE)
(SUM(?DB_PI21_11)     as ?DB_PI21_11_GE)
(?PP_PI21_11_GE+?DB_PI21_11_GE as ?SUM_PPDB_PI21_11_GE)
{
    ?ge a my:Neighborhood ;
        rdfs:label ?ge_label ;
        myd:district_str ?district_label ;
    .
    ?mi_sec myd:neighborhood ?ge ; myp:neighborhood ?st .
    ?st mypq:inclusion_ratio ?ratio ; myps:neighborhood ?ge .


    ?voting_pi21_11 a my:Voting ;
              myd:main_election election:pi2021_11 ;
              myd:section/myd:matched_section/myd:section ?mi_sec ;
              myd:voters_count ?voters_pi21_11 ;
              myd:voters_voted_count ?voted_pi21_11 ;
              myp:vote [mypq:valid_votes_recieved ?pp_21_11   ; myps:vote/myd:party/myd:party wd:Q108601789 ],
                       [mypq:valid_votes_recieved ?db_21_11   ; myps:vote/myd:party/myd:party wd:Q62808154 ]
    .

    ?voting_pi22 a my:Voting ;
              myd:main_election election:pi2022 ;
              myd:section/myd:matched_section/myd:section ?mi_sec ;
              myd:voters_count ?voters_pi22 ;
              myd:voters_voted_count ?voted_pi22 ;
              myp:vote [mypq:valid_votes_recieved ?pp_22   ; myps:vote/myd:party/myd:party wd:Q108601789 ],
                       [mypq:valid_votes_recieved ?db_22   ; myps:vote/myd:party/myd:party wd:Q62808154 ]
    .

    ?voting_pi23 a my:Voting ;
              myd:main_election election:pi2023 ;
              myd:section/myd:matched_section/myd:section ?mi_sec ;
              myd:voters_count ?voters_pi23 ;
              myd:voters_voted_count ?voted_pi23 ;
              myp:vote [mypq:valid_votes_recieved ?ppdb_23   ; myps:vote/myd:party* wd:Q116769868 ]
    .


    bind(floor(?voters_pi21_11*?ratio) as ?VOTERS_PI21_11)
    bind(floor(?voters_pi22*?ratio)    as ?VOTERS_PI22)
    bind(floor(?voters_pi23*?ratio)    as ?VOTERS_PI23)

    bind(floor(?voted_pi21_11*?ratio)  as ?VOTED_PI21_11)
    bind(floor(?voted_pi22*?ratio)     as ?VOTED_PI22)
    bind(floor(?voted_pi23*?ratio)     as ?VOTED_PI23)

    bind(floor(?ppdb_23*?ratio)     as ?PPDB_PI23)
    bind(floor(?pp_22*?ratio)     as ?PP_PI22)
    bind(floor(?db_22*?ratio)     as ?DB_PI22)
    bind(floor(?db_21_11*?ratio)     as ?DB_PI21_11)
    bind(floor(?pp_21_11*?ratio)     as ?PP_PI21_11)

}
group by ?ge ?ge_label ?district_label
having(?VOTERS_PI23_GE>1000)