BASE  <https://elections.ontotext.com/resource/>
PREFIX myp: <https://elections.ontotext.com/resource/prop/indirect/>
PREFIX myd: <https://elections.ontotext.com/resource/prop/direct/>
PREFIX election: <https://elections.ontotext.com/resource/election/>
PREFIX qb: <http://purl.org/linked-data/cube#>
PREFIX myc: <https://elections.ontotext.com/resource/prop/cube/>
PREFIX myps: <https://elections.ontotext.com/resource/prop/statement/>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX mypq: <https://elections.ontotext.com/resource/prop/qualifier/>
CLEAR SILENT GRAPH <graph/cube/municipality-profiling/ppdb> ;
INSERT {
  graph <graph/cube/municipality-profiling/ppdb> {
  ?VOTES_URI a qb:Observation , myc:Voting ;
             qb:dataSet <cube/municipality-profiling> ;
             myc:locality ?locality ;
             myc:candidate ?candidate ;
             myc:votes ?SUM_VOTES ;
   .
  }
} where {
   bind("ppdb" as ?candidate)
   {select ?locality (sum(?VOTES) as ?SUM_VOTES) {
        ?voting myp:vote ?vote_ppdb ;
                  myd:voters_count ?voters_count ;
                  myd:voters_voted_count ?voted_count ;
                  myd:main_election election:pi2023 ;
                  myd:section ?section ;
        .
       ?section  myd:place/myd:municipality ?locality .

        ?vote_ppdb myps:vote/myd:party/myd:party wd:Q116769868;
                  mypq:valid_votes_recieved ?VOTES ;
    .
        } group by ?locality }
      bind(uri(concat("cube/votes/",str(sha1(concat(str(?locality),str(?candidate),str(election:pi2023)))))) as ?VOTES_URI)
}