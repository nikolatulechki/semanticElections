BASE  <https://elections.ontotext.com/resource/>
PREFIX qb: <http://purl.org/linked-data/cube#>
PREFIX myc: <https://elections.ontotext.com/resource/prop/cube/>
PREFIX my: <https://elections.ontotext.com/resource/entity/>
PREFIX myd: <https://elections.ontotext.com/resource/prop/direct/>
PREFIX election: <https://elections.ontotext.com/resource/election/>
CLEAR SILENT GRAPH <graph/cube/sofia-profiling/swing> ;
INSERT {
  GRAPH <graph/cube/sofia-profiling/swing>  {
  ?VOTES_URI a qb:Observation , myc:Voting ;
            qb:dataSet <cube/sofia-profiling> ;
            myc:locality ?sec ;
            myc:candidate ?candidate ;
            myc:votes ?swing ;
   .
  }
}
WHERE {
    {select distinct ?sec  where {?sec a my:Section ; ^myc:locality/qb:dataSet <cube/sofia-profiling> .}}
    bind("swing" as ?candidate)
    [myc:locality ?sec ; myc:candidate "gerb-core" ; myc:votes ?gerb_core] .
    [myc:locality ?sec ; myc:candidate "ppdb" ; myc:votes ?ppdb_core] .
    [myc:locality ?sec ; myc:candidate "socleft-core" ; myc:votes ?socleft] .
    [myc:locality ?sec ; myc:candidate "patriot-core" ; myc:votes ?patriot] .
    [myc:locality ?sec ; myc:candidate "unvoted-core" ; myc:votes ?unvoted_core] .
    ?v myd:section ?sec ; myd:voters_count ?voters .
    bind(?voters - (?gerb_core+?ppdb_core+?patriot+?socleft+?unvoted_core) as ?swing)
    bind(uri(concat("cube/votes/",str(sha1(concat(str(?sec),str(?candidate),str(election:pi2023)))))) as ?VOTES_URI)
}