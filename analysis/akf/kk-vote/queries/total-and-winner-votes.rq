PREFIX my: <https://elections.ontotext.com/resource/entity/>
PREFIX myd: <https://elections.ontotext.com/resource/prop/direct/>
PREFIX election: <https://elections.ontotext.com/resource/election/>
PREFIX risky_model: <https://elections.ontotext.com/resource/risky_model/>
PREFIX jurisdiction: <https://elections.ontotext.com/resource/jurisdiction/>
select ?election ?total_voters ?total_voted ?all_sections
(sum(?voters_risky) as ?total_risky_voters)
(sum(?voted_risky) as ?total_risky_voted)
(count(?v) as ?risky_sections)
(?total_risky_voted/?total_voted as ?risky_ratio)
(?total_risky_voted/?total_risky_voters as ?risky_activity)
(?total_voted/?total_voters as ?total_activity)
{
    {
        select
        ?election
        (sum(?voters) as ?total_voters)
        (sum(?voted) as ?total_voted)
        (count(?v) as ?all_sections)
        where {
            ?election myd:type "parliamentary" .
            ?v a my:Voting ;
               myd:section ?sec ;
               myd:main_election ?election;
               myd:voters_voted_count ?voted ;
               myd:voters_count ?voters_list ;
               .
            optional{
                ?v myd:voters_additional_count ?voters_additional .
            }
            bind(if(bound(?voters_additional),?voters_additional,0) as ?voters_additional_ok)
            bind(?voters_list + ?voters_additional_ok as ?voters)
            filter not exists {
                ?v myd:election/myd:jurisdiction jurisdiction:32 .
            }
        } group by ?election
    }
    ?v a my:Voting ;
       myd:section ?sec ;
       myd:main_election ?election ;
       myd:voters_voted_count ?voted_risky  ;
       myd:voters_count ?voters_list ;
       .
    optional{
        ?v myd:voters_additional_count ?voters_additional .
    }
    bind(if(bound(?voters_additional),?voters_additional,0) as ?voters_additional_ok)
    bind(?voters_list + ?voters_additional_ok as ?voters_risky)
    ?sec myd:matched_section/myd:risk_model risky_model:akf_all_time_risky ;
                            }
group by ?election ?total_voters ?total_voted ?all_sections
order by desc(?election)